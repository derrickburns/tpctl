local common = import '../../lib/common.jsonnet';
local lib = import '../../lib/lib.jsonnet';
local p = import '../../lib/policy.jsonnet';
local tplib = import 'lib.libjsonnet';

{
  dataBucket(config, namespace):: 'tidepool-%s-%s-data' % [config.cluster.metadata.name, namespace],

  assetBucket(config, namespace):: 'tidepool-%s-%s-asset' % [config.cluster.metadata.name, namespace],

  withBucketPolicy(me, bucket)::
    if tplib.isShadow(me)
    then $.withBucketReadingPolicy(bucket)
    else $.withBucketWritingPolicy(bucket),

  withEmailPolicy():: p.attachPolicy([p.statement('*', 'ses:*')]),

  withBucketWritingPolicy(bucket):: p.attachPolicy([
    p.statement(p.bucketArn(bucket), 's3:ListBucket'),
    p.statement(p.contentsArn(bucket), ['s3:GetObject', 's3:PutObject', 's3:DeleteObject']),
  ]),


  withBucketReadingPolicy(bucket):: p.attachPolicy([
    p.statement(p.bucketArn(bucket), 's3:ListBucket'),
    p.statement(p.contentsArn(bucket), 's3:GetObject'),
  ]),
}
